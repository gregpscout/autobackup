*** Settings ***
Library           SSHLibrary
Library           Selenium2Library
Library           MongoDBLibrary
Library           ImapLibrary
Resource          ../../keywords/tbe/ui_tbe_keywords.txt    
Resource          ../../keywords/db/db_Retrieve_Job_From_Mongodb.txt
Resource          ../../keywords/db/sync_tasks.txt 
Resource          ../../resources/settings.txt
Resource          ../../keywords/common/ui_common_keywords.txt   
Variables         ../../models/Requisition_Models.py  
Variables         ../../models/Candidate_Models.py
Variables         ../../models/Candidate_Rejection.py
Variables         ../../models/Email_Templates.py
Suite Teardown    Close Browser

*** Variables ***
${syncTBEJobs}  ./manage.py --sync sync-taleo-requisitions
${needhelp_tbe}  Scout for Taleo Business Edition - Go Scout Go    
${needhelp_text}  Scout for Taleo Business Edition
${create_subject}  Your job has been posted to Scout for Taleo:
${create_content}  Your job has been successfully posted to Scout for Taleo:

*** Test Cases ***

Scenario: Test Create Requisition in TBE for Bullhorn
  [Documentation]  Creates jobs for Bullhorn testing in TBE
  ...  Runs sync and waits for 4 mins for the sync to complete
  ...  Verifies that job is created in the database
  [Tags]  JOBCREATION_BH  SMOKE
    Given User is on TBE Site  ${TBE}  ${BROWSER}
    When Login to TBE  ${TBE_USERNAME}  ${TBE_PASSWORD}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    When Create New Requisition in TBE  ${TBE_REQ1}
    When Create New Requisition in TBE  ${TBE_REQ2}
    ${jobId}=  Get Job Req Id  ${TBE_REQ1['title']}
    Run Sync Jobs  ${HOST_STAGING}  ${syncTBEJobs} 
    Sleep  4m
    Retrieve JobId With Desired Fields From Mongodb  ${jobId}  ${TBE_REQ1['title']}

Scenario: Test Create Requisition in TBE for PCR
  [Documentation]  Creates jobs for PCR testing in TBE
  ...  Runs sync and waits for 4 mins for the sync to complete
  ...  Verifies that job is created in the database
  [Tags]  JOBCREATION_PCR  SMOKE
    Given User is on TBE Site  ${TBE}  ${BROWSER}
    When Login to TBE  ${TBE_USERNAME}  ${TBE_PASSWORD}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    When Create New Requisition in TBE  ${TBE_REQ4}
    When Create New Requisition in TBE  ${TBE_REQ5}
    ${jobId}=  Get Job Req Id  ${TBE_REQ4['title']}
    Run Sync Jobs  ${HOST_TEST}  ${syncTBEJobs}
    Sleep  4m
    Retrieve JobId With Desired Fields From Mongodb  ${jobId}  ${TBE_REQ1['title']}    
    
Scenario: Test Candidate Sharing and Acceptance in TBE Staging for Bullhorn
  [Documentation]  Shares candidate and accepts the same candidate in TBE
  [Tags]  ACCEPTSUBMISSION_BH  SMOKE  SHARE
    Given User is on TBE Site  ${TBE}  ${BROWSER} 
    When Login to TBE  ${TBE_USERNAME}  ${TBE_PASSWORD}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    When Go to Requisitions Page in TBE
    Then Go to Scout Candidates List for Job  ${TBE_REQ1['title']}  staging
    And Verify Fee Percentage for the candidate submitted in TBE  ${BHS_Accept_Candidate}  staging  
    And Share Candidate in TBE  ${BHS_Accept_Candidate['mod_name']}  ${SCOUT_EMAIL}  staging 
    And Accept Candidate in TBE  ${BHS_Accept_Candidate['mod_name']}  ${TBE_REQ1['title']}  staging

Scenario: Test Candidate Sharing and Acceptance in TBE Test for PCR
  [Documentation]  Shares candidate and accepts the same candidate in TBE
  [Tags]  ACCEPTSUBMISSION_PCR  SMOKE  SHARE
    Given User is on TBE Site  ${TBE}  ${BROWSER} 
    When Login to TBE  ${TBE_USERNAME_TEST}  ${TBE_PASSWORD_TEST}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    When Go to Requisitions Page in TBE
    Then Go to Scout Candidates List for Job  ${TBE_REQ4['title']}  test
    And Verify Fee Percentage for the candidate submitted in TBE  ${PCR_Accept_Candidate}  test
    And Share Candidate in TBE  ${PCR_Accept_Candidate['mod_name']}  ${SCOUT_EMAIL}  test 
    And Accept Candidate in TBE  ${PCR_Accept_Candidate['mod_name']}  ${TBE_REQ4['title']}  test     
    
Scenario: Test Candidate Rejection in TBE for Candidate Qualifications for Bullhorn
  [Documentation]  Rejects candidates for a job in TBE with reason Candidate Qualifications
  [Tags]  REJECTSUBMISSION_BH  SMOKE
    Given User is on TBE Site  ${TBE}  ${BROWSER} 
    When Login to TBE  ${TBE_USERNAME}  ${TBE_PASSWORD}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    When Go to Requisitions Page in TBE
    Then Go to Scout Candidates List for Job  ${TBE_REQ1['title']}  staging
    And Verify Fee Percentage for the candidate submitted in TBE  ${BHS_Reject_Candidate}  staging
    And Reject Candidate in TBE  ${BHS_Reject_Candidate['mod_name']}  ${TBE_Rejection_Reasons['Candidate_Qualifications']}  ${TBE_Rejection_Comments['Candidate_Qualifications']}  staging       
    
Scenario: Test Candidate Rejection in TBE due to Placement Fee for PCR
  [Documentation]  Rejects candidates for a job in TBE with reason Placement Fee
  [Tags]  REJECTSUBMISSION_PCR  REGRESSION  
    Given User is on TBE Site  ${TBE}  ${BROWSER} 
    When Login to TBE  ${TBE_USERNAME_TEST}  ${TBE_PASSWORD_TEST}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    When Go to Requisitions Page in TBE
    Then Go to Scout Candidates List for Job  ${TBE_REQ4['title']}  test
    And Verify Fee Percentage for the candidate submitted in TBE  ${PCR_Reject_Candidate}  test
    And Reject Candidate in TBE  ${PCR_Reject_Candidate['mod_name']}  ${TBE_Rejection_Reasons['Placement_Fee']}  ${TBE_Rejection_Comments['Placement_Fee']}  test       
        
Scenario: Test Candidate Rejection in TBE for Other Reason
  [Documentation]  Rejects candidates for a job in TBE with reason Other
  [Tags]  REJECTSUBMISSION  REGRESSION  
    Given User is on TBE Site  ${TBE}  ${BROWSER} 
    When Login to TBE  ${TBE_USERNAME}  ${TBE_PASSWORD}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    When Go to Requisitions Page in TBE
    Then Go to Scout Candidates List for Job  ${TBE_REQ1['title']}  staging
    And Reject Candidate in TBE  ${BHS_Reject_Candidate['mod_name']}  ${TBE_Rejection_Reasons['Other']}  ${TBE_Rejection_Comments['Other']}  staging  
    
Scenario: Test Links in TBE
  [Documentation]  Validates the following links in TBE
  ...  Test feedback link
  [Tags]  LINKS  REGRESSION
    Given User is on TBE Site  ${TBE}  ${BROWSER} 
    When Login to TBE  ${TBE_USERNAME}  ${TBE_PASSWORD}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    When Go to Requisitions Page in TBE
    Then Go to Scout Candidates List for Job  ${TBE_REQ1['title']}  staging
    Select Frame in TBE  staging
    Test Feedback Link on Scout  ${TBE_FEEDBACK}  ${STAGING_EMAIL_ID}  ${STAGING_EMAIL_PWD}  ${NOREPLY_STAGING_EMAIL}     
    And Sleep  5s
    And Test Need Help with Scout Link  ${needhelp_tbe}  ${needhelp_text}

Scenario: Test Placing Job on Hold in TBE
  [Tags]  JOBHOLD
    Given User is on TBE Site  ${TBE}  ${BROWSER}
    When Login to TBE  ${TBE_USERNAME}  ${TBE_PASSWORD}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    When Create New Requisition in TBE  ${TBE_REQ3}
    Run Sync Jobs  ${HOST_STAGING}  ${syncTBEJobs}
    Sleep  4m
    ${jobId}  Place Existing Requisition on Hold in TBE  ${TBE_REQ3['title']} 
    Run Sync Jobs  ${HOST_STAGING}  ${syncTBEJobs} 
    Sleep  4m
    Verify Job is on Hold in Mongodb  ${jobId}


# Before running any of the tests below, make sure that job requisitions TBE_REQ1 and TBE_REQ2 already exist in TBE
# If not, run "JOBCREATION_BH" before running any of the following tests    

Scenario: Test Editing Non Closure fields for a Job in TBE
  [Tags]  JOB_EDIT  EDIT_NON_CLOSURE
    Given User is on TBE Site  ${TBE}  ${BROWSER}
    When Login to TBE  ${TBE_USERNAME}  ${TBE_PASSWORD}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    ${jobId}=  Edit Job Title Location and Education for an Existing Job in TBE  ${TBE_REQ1['title']}  ${EDIT_REQ1} 
    Run Sync Jobs  ${HOST_STAGING}  ${syncTBEJobs} 
    Sleep  4m
    Verify Job Title in Mongodb  ${jobId}  ${EDIT_REQ1['title']}  1

Scenario: Test Editing Closure fields for a Job in TBE
  [Tags]  JOB_EDIT  EDIT_CLOSURE
    Given User is on TBE Site  ${TBE}  ${BROWSER}
    When Login to TBE  ${TBE_USERNAME}  ${TBE_PASSWORD}  ${TBE_COMPANY}
    Then Verify user is logged in to TBE
    ${jobId}=  Change Placement Fee for an Existing TBE Requisition  ${TBE_REQ2['title']}  27% 
    Run Sync Jobs  ${HOST_STAGING}  ${syncTBEJobs}
    Sleep  4m 
    Verify Edited Job is Reopened in Scout  ${jobId}  ${TBE_REQ2['title']}


    
	
	

