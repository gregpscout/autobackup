*** Keywords ***
User is on TEE Site  
  [Arguments]  ${Url}  ${Browser}  
	Open Browser  ${Url}  ${Browser}
	Maximize Browser Window
	
Login to TEE
  [Arguments]  ${username}  ${password}
    Input Text  dialogTemplate-dialogForm-content-login-name1  ${username}
    Input Text  dialogTemplate-dialogForm-content-login-password  ${password}
    Click Link  dialogTemplate-dialogForm-content-login-defaultCmd

Go to Scout iframe in TEE after Impersonation
    Wait Until Keyword Succeeds  10  2  TPX Admin Login Page Should Be Open
    Go To  https://staging.talentpx.com/tee/#/positions
    Verify User is on Scout Iframe for TEE

Go to Candidates page for job in TEE
  [Arguments]  ${jobTitle}
    Click Link  ${jobTitle}
    Verify User is on Candidates List Page  ${jobTitle}

Go To Candidate Review Page in TEE
  [Arguments]  ${Candidate_Obj}
    Verify Candidate is Available for Review  ${Candidate_Obj}
    Click Link  ${Candidate_Obj['mod_name']}

Verify Candidate is Available for Review
  [Arguments]  ${Candidate_Obj}
    Wait Until Keyword Succeeds  20  2  Table Column Should Contain  submissions-table  1  ${Candidate_Obj['mod_name']}
    Wait Until Keyword Succeeds  20  2  Table Column Should Contain  submissions-table  3  ${Candidate_Obj['fee']}

Share Candidate in TEE
  [Arguments]  ${Candidate_Obj}  ${email}
    Go To Candidate Review Page in TEE  ${Candidate_Obj}
    Verify User is on Candidate Details Page in TEE  ${Candidate_Obj['mod_name']}
    Wait Until Keyword Succeeds  10  1  Click Element  share-candidate
    Wait Until Page Contains Element  share-modal
    Wait Until Keyword Succeeds  20  1  Input Text  share-modal__to  ${email}
    Click Link  share-modal__send
    Verify User is on Candidate Details Page in TEE  ${Candidate_Obj['mod_name']}

Accept Candidate in TEE
  [Arguments]  ${Candidate_Obj}  ${jobTitle}
    Verify User is on Candidate Details Page in TEE  ${Candidate_Obj['mod_name']}
    Click Element  interested
    Wait Until Keyword Succeeds  20  2  Page Should Contain Element  candidate-exists-error
    Wait Until Keyword Succeeds  60  2  Verify User is on Candidates List Page  ${jobTitle}
    Page Should Not Contain  ${Candidate_Obj['name']}

Reject Candidate in TEE
  [Arguments]  ${Candidate_Obj}  ${reason}  ${comments}
    Go To Candidate Review Page in TEE  ${Candidate_Obj}
    Verify User is on Candidate Details Page in TEE  ${Candidate_Obj['mod_name']}
    Sleep  2s
    Click Element  not-interested
    Wait Until Keyword Succeeds  20  1  Page Should Contain Element  not-interested-modal
    Wait Until Keyword Succeeds  20  4  Select Rejection Reason for TEE  ${reason}
    Input Text  not-interested-modal__comments  ${comments}
    Click Element  not-interested-modal__submit

Select Rejection Reason for TEE
  [Arguments]  ${reason}
  [Documentation]  Takes qualifications or fee or other as arguments
    Run Keyword If  '${reason}' == 'qualifications'  Select Radio Button  not-interested-reason  0
    Run Keyword If  '${reason}' == 'fee'  Select Radio Button  not-interested-reason  1
    Run Keyword If  '${reason}' == 'other'  Select Radio Button  not-interested-reason  2

Verify Candidate is no Longer Pending Review
  [Arguments]  ${CandidateName}  ${jobTitle}
    Click Link  ${jobTitle}
    Verify User is on Candidates List Page  ${jobTitle}
    Page Should Not Contain  ${CandidateName}

Verify User is on Candidate Details Page in TEE
  [Arguments]  ${CandidateName}
    Wait Until Page Contains  ${CandidateName}  20s
    Wait Until Page Contains Element  interested  10s
    Wait Until Page Contains Element  not-interested  10s
    Wait Until Page Contains Element  share-candidate  10s

Verify Duplicate Candidate Message in TEE
  [Arguments]  ${Candidate_Obj}
    Wait Until Keyword Succeeds  20  2  Element Should Contain  candidate-exists-error  ${Candidate_Obj['name']} (${Candidate_Obj['email']}) already exists in your Taleo system. Do you want to continue?
    #Wait Until Keyword Succeeds  20  2  Element Should Contain  candidate-exists-error  ${Candidate_Obj['name']}
    #Wait Until Keyword Succeeds  20  2  Element Should Contain  candidate-exists-error  ${Candidate_Obj['email']}

Verify User is on Candidates List Page
  [Arguments]  ${jobTitle}
    Wait Until Keyword Succeeds  10  2  Element Should Be Visible  submissions-table
    Page Should Contain  ${jobTitle}

Go to Candidate Details Page from Scout Candidates List
  [Arguments]  ${CandidateName}
    Wait Until Keyword Succeeds  10  2  Page Should Contain Element  xpath=//*[@class="candidateList"]
    Click Link  ${CandidateName}
    Verify User is on Candidate Details Page
